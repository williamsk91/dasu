The expressive power of the class of procedures that we can define at
this point is very limited, because we have no way to make tests and to
perform different operations depending on the result of a test. For
instance, we cannot define a procedure that computes the absolute value
of a number by testing whether the number is positive, negative, or zero
and taking different actions in the different cases according to the
rule

![|x|; = ;\left{ \begin{matrix}
x & {;\text{if}} & {x > 0,} 
0 & {;\text{if}} & {x = 0,} 
{- x} & {;\text{if}} & {x < 0.} 
\end{matrix} \right.](https://latex.codecogs.com/png.latex?%7Cx%7C%5C%3B%20%3D%20%5C%3B%5Cleft%5C%7B%20%5Cbegin%7Bmatrix%7D%0Ax%20%26%20%7B%5C%3B%5Ctext%7Bif%7D%7D%20%26%20%7Bx%20%3E%200%2C%7D%20%5C%5C%0A0%20%26%20%7B%5C%3B%5Ctext%7Bif%7D%7D%20%26%20%7Bx%20%3D%200%2C%7D%20%5C%5C%0A%7B-%20x%7D%20%26%20%7B%5C%3B%5Ctext%7Bif%7D%7D%20%26%20%7Bx%20%3C%200.%7D%20%5C%5C%0A%5Cend%7Bmatrix%7D%20%5Cright. "|x|; = ;\\left{ \begin{matrix}
x & {;\text{if}} & {x > 0,} 
0 & {;\text{if}} & {x = 0,} 
{- x} & {;\text{if}} & {x < 0.} 
\end{matrix} \right.")

This construct is called a _case analysis_, and there is a special form
in Lisp for notating such a case analysis. It is called `cond` (which
stands for “conditional”), and it is used as follows:

```lisp
(define (abs x)
  (cond ((> x 0) x)
        ((= x 0) 0)
        ((< x 0) (- x))))
```

The general form of a conditional expression is

```lisp
(cond (⟨p₁⟩ ⟨e₁⟩)
      (⟨p₂⟩ ⟨e₂⟩)
      …
      (⟨pₙ⟩ ⟨eₙ⟩))
```

consisting of the symbol `cond` followed by parenthesized pairs of
expressions

```lisp
(⟨p⟩ ⟨e⟩)
```

called _clauses_. The first expression in each pair is a
_predicate_—that is, an expression whose value is interpreted as either
true or false.[^¹⁷]

Conditional expressions are evaluated as follows. The predicate
![\langle p_{1}\rangle](https://latex.codecogs.com/png.latex?%5Clangle%20p_%7B1%7D%5Crangle "\\langle p_{1}\rangle")
is evaluated first. If its value is false, then
![\langle p_{2}\rangle](https://latex.codecogs.com/png.latex?%5Clangle%20p_%7B2%7D%5Crangle "\\langle p_{2}\rangle")
is evaluated. If
![\langle p_{2}\rangle](https://latex.codecogs.com/png.latex?%5Clangle%20p_%7B2%7D%5Crangle "\\langle p_{2}\rangle")’s
value is also false, then
![\langle p_{3}\rangle](https://latex.codecogs.com/png.latex?%5Clangle%20p_%7B3%7D%5Crangle "\\langle p_{3}\rangle")
is evaluated. This process continues until a predicate is found whose
value is true, in which case the interpreter returns the value of the
corresponding _consequent expression_
![\langle e\rangle](https://latex.codecogs.com/png.latex?%5Clangle%20e%5Crangle "\\langle e\rangle")
of the clause as the value of the conditional expression. If none of the
![\langle p\rangle](https://latex.codecogs.com/png.latex?%5Clangle%20p%5Crangle "\\langle p\rangle")’s
is found to be true, the value of the `cond` is undefined.

The word _predicate_ is used for procedures that return true or false,
as well as for expressions that evaluate to true or false. The
absolute-value procedure `abs` makes use of the primitive predicates
`>`, `<`, and `=`.[^¹⁸] These take two numbers as arguments and
test whether the first number is, respectively, greater than, less than,
or equal to the second number, returning true or false accordingly.

Another way to write the absolute-value procedure is

```lisp
(define (abs x)
  (cond ((< x 0) (- x))
        (else x)))
```

which could be expressed in English as “If
![x](https://latex.codecogs.com/png.latex?x "x") is less than zero
return ![- x](https://latex.codecogs.com/png.latex?-%20x "- x");
otherwise return ![x](https://latex.codecogs.com/png.latex?x "x").”
`Else` is a special symbol that can be used in place of the
![\langle p\rangle](https://latex.codecogs.com/png.latex?%5Clangle%20p%5Crangle "\\langle p\rangle")
in the final clause of a `cond`. This causes the `cond` to return as its
value the value of the corresponding
![\langle e\rangle](https://latex.codecogs.com/png.latex?%5Clangle%20e%5Crangle "\\langle e\rangle")
whenever all previous clauses have been bypassed. In fact, any
expression that always evaluates to a true value could be used as the
![\langle p\rangle](https://latex.codecogs.com/png.latex?%5Clangle%20p%5Crangle "\\langle p\rangle")
here.

Here is yet another way to write the absolute-value procedure:

```lisp
(define (abs x)
  (if (< x 0)
      (- x)
      x))
```

This uses the special form `if`, a restricted type of conditional that
can be used when there are precisely two cases in the case analysis. The
general form of an `if` expression is

```lisp
(if ⟨predicate⟩ ⟨consequent⟩ ⟨alternative⟩)
```

To evaluate an `if` expression, the interpreter starts by evaluating the
` ⟨``predicate``⟩ ` part of the expression. If the ` ⟨``predicate``⟩ `
evaluates to a true value, the interpreter then evaluates the
` ⟨``consequent``⟩ ` and returns its value. Otherwise it evaluates the
` ⟨``alternative``⟩ ` and returns its value.[^¹⁹]

In addition to primitive predicates such as `<`, `=`, and `>`, there are
logical composition operations, which enable us to construct compound
predicates. The three most frequently used are these:

- `(and ⟨e₁⟩ … ⟨eₙ⟩)`

  The interpreter evaluates the expressions ` ⟨``e``⟩ ` one at a time,
  in left-to-right order. If any ` ⟨``e``⟩ ` evaluates to false, the
  value of the `and` expression is false, and the rest of the
  ` ⟨``e``⟩ `’s are not evaluated. If all ` ⟨``e``⟩ `’s evaluate to true
  values, the value of the `and` expression is the value of the last
  one.

- `(or ⟨e₁⟩ … ⟨eₙ⟩)`

  The interpreter evaluates the expressions ` ⟨``e``⟩ ` one at a time,
  in left-to-right order. If any ` ⟨``e``⟩ ` evaluates to a true value,
  that value is returned as the value of the `or` expression, and the
  rest of the ` ⟨``e``⟩ `’s are not evaluated. If all ` ⟨``e``⟩ `’s
  evaluate to false, the value of the `or` expression is false.

- `(not ⟨e⟩)`

  The value of a `not` expression is true when the expression
  ` ⟨``e``⟩ ` evaluates to false, and false otherwise.

Notice that `and` and `or` are special forms, not procedures, because
the subexpressions are not necessarily all evaluated. `Not` is an
ordinary procedure.

As an example of how these are used, the condition that a number
![x](https://latex.codecogs.com/png.latex?x "x") be in the range
![5 < x < 10](https://latex.codecogs.com/png.latex?5%20%3C%20x%20%3C%2010 "5 < x < 10")
may be expressed as

```lisp
(and (> x 5) (< x 10))
```

As another example, we can define a predicate to test whether one number
is greater than or equal to another as

```lisp
(define (>= x y)
  (or (> x y) (= x y)))
```

or alternatively as

```lisp
(define (>= x y)
  (not (< x y)))
```

> **Exercise 1.1:** Below is a sequence of expressions. What is the
> result printed by the interpreter in response to each expression?
> Assume that the sequence is to be evaluated in the order in which it
> is presented.
>
> ```lisp
> 10
> (+ 5 3 4)
> (- 9 1)
> (/ 6 2)
> (+ (* 2 4) (- 4 6))
> (define a 3)
> (define b (+ a 1))
> (+ a b (* a b))
> (= a b)
> (if (and (> b a) (< b (* a b)))
>     b
>     a)
> (cond ((= a 4) 6)
>       ((= b 4) (+ 6 7 a))
>       (else 25))
> (+ 2 (if (> b a) b a))
> (* (cond ((> a b) a)
>          ((< a b) b)
>          (else -1))
>    (+ a 1))
> ```

> **Exercise 1.2:** Translate the following expression into prefix form:
>
> ![\frac{5 + 4 + (2 - (3 - (6 + \frac{4}{5})))}{3(6 - 2)(2 - 7)}.](https://latex.codecogs.com/png.latex?%5Cfrac%7B5%20%2B%204%20%2B%20%282%20-%20%283%20-%20%286%20%2B%20%5Cfrac%7B4%7D%7B5%7D%29%29%29%7D%7B3%286%20-%202%29%282%20-%207%29%7D. "\\frac{5 + 4 + (2 - (3 - (6 + \frac{4}{5})))}{3(6 - 2)(2 - 7)}.")

> **Exercise 1.3:** Define a procedure that takes three numbers as
> arguments and returns the sum of the squares of the two larger
> numbers.

> **Exercise 1.4:** Observe that our model of evaluation allows for
> combinations whose operators are compound expressions. Use this
> observation to describe the behavior of the following procedure:
>
> ```lisp
> (define (a-plus-abs-b a b)
>   ((if (> b 0) + -) a b))
> ```

> **Exercise 1.5:** Ben Bitdiddle has invented a test to determine
> whether the interpreter he is faced with is using applicative-order
> evaluation or normal-order evaluation. He defines the following two
> procedures:
>
> ```lisp
> (define (p) (p))
>
> (define (test x y)
>   (if (= x 0)
>       0
>       y))
> ```
>
> Then he evaluates the expression
>
> ```lisp
> (test 0 (p))
> ```
>
> What behavior will Ben observe with an interpreter that uses
> applicative-order evaluation? What behavior will he observe with an
> interpreter that uses normal-order evaluation? Explain your answer.
> (Assume that the evaluation rule for the special form `if` is the same
> whether the interpreter is using normal or applicative order: The
> predicate expression is evaluated first, and the result determines
> whether to evaluate the consequent or the alternative expression.)

[^¹⁷] “Interpreted as either true or false” means this: In
Scheme, there are two distinguished values that are denoted by the
constants `#t` and `#f`. When the interpreter checks a predicate’s
value, it interprets `#f` as false. Any other value is treated as true.
(Thus, providing `#t` is logically unnecessary, but it is convenient.)
In this book we will use names `true` and `false`, which are associated
with the values `#t` and `#f` respectively.

[^¹⁸]:
    `Abs` also uses the “minus” operator `-`, which, when used
    with a single operand, as in `(- x)`, indicates negation.

[^¹⁹]:
    A minor difference between `if` and `cond` is that the
    ` ⟨``e``⟩ ` part of each `cond` clause may be a sequence of expressions.
    If the corresponding ` ⟨``p``⟩ ` is found to be true, the expressions
    ` ⟨``e``⟩ ` are evaluated in sequence and the value of the final
    expression in the sequence is returned as the value of the `cond`. In an
    `if` expression, however, the ` ⟨``consequent``⟩ ` and ` ⟨``alternative``⟩ `
    must be single expressions.
