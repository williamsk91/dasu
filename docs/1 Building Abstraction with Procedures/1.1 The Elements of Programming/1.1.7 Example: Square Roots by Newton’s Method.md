Procedures, as introduced above, are much like ordinary mathematical
functions. They specify a value that is determined by one or more
parameters. But there is an important difference between mathematical
functions and computer procedures. Procedures must be effective.

As a case in point, consider the problem of computing square roots. We
can define the square-root function as

![\sqrt{x};; = ;;{\text{the};; y};;\text{such\ that};;{y \geq 0};;{\text{and};; y^{2} = x.}](https://latex.codecogs.com/png.latex?%5Csqrt%7Bx%7D%5C%3B%5C%3B%20%3D%20%5C%3B%5C%3B%7B%5Ctext%7Bthe%7D%5C%3B%5C%3B%20y%7D%5C%3B%5C%3B%5Ctext%7Bsuch%5C%20that%7D%5C%3B%5C%3B%7By%20%5Cgeq%200%7D%5C%3B%5C%3B%7B%5Ctext%7Band%7D%5C%3B%5C%3B%20y%5E%7B2%7D%20%3D%20x.%7D "\\sqrt{x};; = ;;{\text{the};; y};;\text{such\ that};;{y \geq 0};;{\text{and};; y^{2} = x.}")

This describes a perfectly legitimate mathematical function. We could
use it to recognize whether one number is the square root of another, or
to derive facts about square roots in general. On the other hand, the
definition does not describe a procedure. Indeed, it tells us almost
nothing about how to actually find the square root of a given number. It
will not help matters to rephrase this definition in pseudo-Lisp:

```lisp
(define (sqrt x)
  (the y (and (>= y 0)
              (= (square y) x))))
```

This only begs the question.

The contrast between function and procedure is a reflection of the
general distinction between describing properties of things and
describing how to do things, or, as it is sometimes referred to, the
distinction between declarative knowledge and imperative knowledge. In
mathematics we are usually concerned with declarative (what is)
descriptions, whereas in computer science we are usually concerned with
imperative (how to) descriptions.[^²⁰]

How does one compute square roots? The most common way is to use
Newton’s method of successive approximations, which says that whenever
we have a guess ![y](https://latex.codecogs.com/png.latex?y "y") for the
value of the square root of a number
![x](https://latex.codecogs.com/png.latex?x "x"), we can perform a
simple manipulation to get a better guess (one closer to the actual
square root) by averaging
![y](https://latex.codecogs.com/png.latex?y "y") with
![x/y](https://latex.codecogs.com/png.latex?x%2Fy "x/y").[^²¹]
For example, we can compute the square root of 2 as follows. Suppose our
initial guess is 1:

```example
Guess     Quotient      Average

1         (2/1)  = 2    ((2 + 1)/2)  = 1.5

1.5       (2/1.5)       ((1.3333 + 1.5)/2)
            = 1.3333      = 1.4167

1.4167    (2/1.4167)    ((1.4167 + 1.4118)/2)
            = 1.4118      = 1.4142

1.4142    ...           ...
```

Continuing this process, we obtain better and better approximations to
the square root.

Now let’s formalize the process in terms of procedures. We start with a
value for the radicand (the number whose square root we are trying to
compute) and a value for the guess. If the guess is good enough for our
purposes, we are done; if not, we must repeat the process with an
improved guess. We write this basic strategy as a procedure:

```lisp
(define (sqrt-iter guess x)
  (if (good-enough? guess x)
      guess
      (sqrt-iter (improve guess x) x)))
```

A guess is improved by averaging it with the quotient of the radicand
and the old guess:

```lisp
(define (improve guess x)
  (average guess (/ x guess)))
```

where

```lisp
(define (average x y)
  (/ (+ x y) 2))
```

We also have to say what we mean by “good enough.” The following will do
for illustration, but it is not really a very good test. (See [Exercise
1.7](#Exercise-1_002e7).) The idea is to improve the answer until it is
close enough so that its square differs from the radicand by less than a
predetermined tolerance (here 0.001):[^²²]

```lisp
(define (good-enough? guess x)
  (< (abs (- (square guess) x)) 0.001))
```

Finally, we need a way to get started. For instance, we can always guess
that the square root of any number is 1:[^²³]

```lisp
(define (sqrt x)
  (sqrt-iter 1.0 x))
```

If we type these definitions to the interpreter, we can use `sqrt` just
as we can use any procedure:

```lisp
(sqrt 9)
3.00009155413138

(sqrt (+ 100 37))
11.704699917758145

(sqrt (+ (sqrt 2) (sqrt 3)))
1.7739279023207892

(square (sqrt 1000))
1000.000369924366
```

The `sqrt` program also illustrates that the simple procedural language
we have introduced so far is sufficient for writing any purely numerical
program that one could write in, say, C or Pascal. This might seem
surprising, since we have not included in our language any iterative
(looping) constructs that direct the computer to do something over and
over again. `Sqrt-iter`, on the other hand, demonstrates how iteration
can be accomplished using no special construct other than the ordinary
ability to call a procedure.[^²⁴]

> **Exercise 1.6:** Alyssa P. Hacker doesn’t see why `if` needs to be
> provided as a special form. “Why can’t I just define it as an ordinary
> procedure in terms of `cond`?” she asks. Alyssa’s friend Eva Lu Ator
> claims this can indeed be done, and she defines a new version of `if`:
>
> ```lisp
> (define (new-if predicate
>                 then-clause
>                 else-clause)
>   (cond (predicate then-clause)
>         (else else-clause)))
> ```
>
> Eva demonstrates the program for Alyssa:
>
> ```lisp
> (new-if (= 2 3) 0 5)
> 5
>
> (new-if (= 1 1) 0 5)
> 0
> ```
>
> Delighted, Alyssa uses `new-if` to rewrite the square-root program:
>
> ```lisp
> (define (sqrt-iter guess x)
>   (new-if (good-enough? guess x)
>           guess
>           (sqrt-iter (improve guess x) x)))
> ```
>
> What happens when Alyssa attempts to use this to compute square roots?
> Explain.

> **Exercise 1.7:** The `good-enough?` test used in computing square
> roots will not be very effective for finding the square roots of very
> small numbers. Also, in real computers, arithmetic operations are
> almost always performed with limited precision. This makes our test
> inadequate for very large numbers. Explain these statements, with
> examples showing how the test fails for small and large numbers. An
> alternative strategy for implementing `good-enough?` is to watch how
> `guess` changes from one iteration to the next and to stop when the
> change is a very small fraction of the guess. Design a square-root
> procedure that uses this kind of end test. Does this work better for
> small and large numbers?

> **Exercise 1.8:** Newton’s method for cube roots is based on the fact
> that if ![y](https://latex.codecogs.com/png.latex?y "y") is an
> approximation to the cube root of
> ![x](https://latex.codecogs.com/png.latex?x "x"), then a better
> approximation is given by the value
>
> ![\frac{{x/y^{2}} + 2y}{3}.](https://latex.codecogs.com/png.latex?%5Cfrac%7B%7Bx%2Fy%5E%7B2%7D%7D%20%2B%202y%7D%7B3%7D. "\\frac{{x/y^{2}} + 2y}{3}.")
>
> Use this formula to implement a cube-root procedure analogous to the
> square-root procedure. (In [1.3.4](1_002e3.xhtml#g_t1_002e3_002e4) we
> will see how to implement Newton’s method in general as an abstraction
> of these square-root and cube-root procedures.)

[^²⁰]:
    Declarative and imperative descriptions are intimately
    related, as indeed are mathematics and computer science. For instance,
    to say that the answer produced by a program is “correct” is to make a
    declarative statement about the program. There is a large amount of
    research aimed at establishing techniques for proving that programs are
    correct, and much of the technical difficulty of this subject has to do
    with negotiating the transition between imperative statements (from
    which programs are constructed) and declarative statements (which can be
    used to deduce things). In a related vein, an important current area in
    programming-language design is the exploration of so-called very
    high-level languages, in which one actually programs in terms of
    declarative statements. The idea is to make interpreters sophisticated
    enough so that, given “what is” knowledge specified by the programmer,
    they can generate “how to” knowledge automatically. This cannot be done
    in general, but there are important areas where progress has been made.
    We shall revisit this idea in [Chapter 4](Chapter-4.xhtml#Chapter-4).

[^²¹]:
    This square-root algorithm is actually a special case of
    Newton’s method, which is a general technique for finding roots of
    equations. The square-root algorithm itself was developed by Heron of
    Alexandria in the first century A.D. We will see how to express the
    general Newton’s method as a Lisp procedure in
    [1.3.4](1_002e3.xhtml#g_t1_002e3_002e4).

[^²²]:
    We will usually give predicates names ending with question
    marks, to help us remember that they are predicates. This is just a
    stylistic convention. As far as the interpreter is concerned, the
    question mark is just an ordinary character.

[^²³]:
    Observe that we express our initial guess as 1.0 rather
    than 1. This would not make any difference in many Lisp implementations.
    MIT Scheme, however, distinguishes between exact integers and decimal
    values, and dividing two integers produces a rational number rather than
    a decimal. For example, dividing 10 by 6 yields 5/3, while dividing 10.0
    by 6.0 yields 1.6666666666666667. (We will learn how to implement
    arithmetic on rational numbers in
    [2.1.1](2_002e1.xhtml#g_t2_002e1_002e1).) If we start with an initial
    guess of 1 in our square-root program, and
    ![x](https://latex.codecogs.com/png.latex?x "x") is an exact integer,
    all subsequent values produced in the square-root computation will be
    rational numbers rather than decimals. Mixed operations on rational
    numbers and decimals always yield decimals, so starting with an initial
    guess of 1.0 forces all subsequent values to be decimals.

[^²⁴]:
    Readers who are worried about the efficiency issues
    involved in using procedure calls to implement iteration should note the
    remarks on “tail recursion” in [1.2.1](1_002e2.xhtml#g_t1_002e2_002e1).
